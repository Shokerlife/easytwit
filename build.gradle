buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.springframework:springloaded:1.2.5.RELEASE")
    classpath("com.moowork.gradle:gradle-node-plugin:0.13")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'com.moowork.node'

idea {
	module {
		inheritOutputDirs = false
		outputDir = file("$buildDir/classes/main/")
	}
}

jar {
	baseName = 'easy-twit'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
  mavenLocal()
}

configurations {
	warOverlays
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.data:spring-data-rest-hal-browser')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.projectlombok:lombok')
	compile('mysql:mysql-connector-java:5.1.34')
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')

  warOverlays('org.pat.easytwit:easytwit-overlay:1.0-SNAPSHOT')
}

task processOverlays(type: Copy) {
  configurations.warOverlays.each { overlay ->
      from zipTree(overlay)
      into "build/overlays"
      include '**/*'
      exclude '**/*css*/**'
  }
}

war {
  dependsOn processOverlays

  from 'build/overlays'
  into '/'
}

task cleanTempDir(type: Delete) {
  delete "build/overlays"
}

war.finalizedBy cleanTempDir

task runNpmBuild(type: NpmTask) {
  args = ['run', 'build']
}

processResources.finalizedBy(runNpmBuild)
